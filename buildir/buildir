#!/usr/bin/perl -w
#
# $Id$
#
# The author disclaims all copyrights and releases this script into the
# public domain.
#
# Like 'mkdir -p', but does 'cvs add' if under CVS sandboxes, or other
# extensible actions. Prints directories created when run with -p
# option, used by other tools, such as a shell function to change the
# working directory to a newly created directory:
#
# function ndir {
#   NDIR=`buildir -p $@ | tail -1`
#   [ $? -ne 0 ] && exit $?
#   builtin cd "$NDIR"
# }

use strict;

use File::Spec ();

use Getopt::Std;
my %opts;
getopts 'h?p', \%opts;

print_help() if $opts{h} or $opts{'?'} or not @ARGV;

my $start_dir = File::Spec->curdir();

for my $dir (@ARGV) {

  my @dirbits = File::Spec->splitdir($dir);

  # start directory build from working directory for relative paths,
  # root directory for absolute paths
  my $parent = $start_dir;
  if ( $dirbits[0] eq '' ) {
    $parent = File::Spec->rootdir();
    shift @dirbits;
  }
  chdir $parent
   or die "error: could not chdir: errno=$!, path=$parent\n";

  # work through dirbits, as not all mkdir support -p, and do special
  # things under CVS and Subversion sandboxes
  #
  # KLUGE // in pathes add empty bits to the list, evict with grep
  for my $dirbit ( grep { defined and $_ ne '' } @dirbits ) {
    goto NEXTDIR if -d $dirbit;

    if ( -f File::Spec->catfile(qw{CVS Root}) ) {
      # CVS: add dir automagically
      mkdir $dirbit
       or die
       "error: could not create directory: errno=$!, name=$dirbit, path=$dir\n";
      system qw{cvs -Q add}, $dirbit;

    } elsif ( -f File::Spec->catfile(qw{.svn entries}) ) {
      # SVN: use mkdir subcommand
      system qw{svn mkdir}, $dirbit;

    } else {
      mkdir $dirbit;

    }

   NEXTDIR: chdir $dirbit
     or die "error: could not chdir: errno=$!, name=$dirbit, path=$dir\n";
  }

  print $dir, $/ if exists $opts{p};
}

sub print_help {
  print <<"HELP";
Usage: $0 [options] dir [dir2 ...]

Creates directories.

Options:
  -h/-?  Display this message.

  -p     Print directories created.

HELP
  exit 100;
}
